-*-------------------------------------------------------------------------------*
-* Copyright (c) Information Builders, Inc. All rights reserved. @MFSM_NOPROLOG@
-*FEX_DESCRIPTION=Legacy Sample: create synonym from sysflow for &APPDIR/&REQ_NAME
-*-------------------------------------------------------------------------------*
-* create synonym from sysflow for &APPDIR/&REQ_NAME
-*
-* the original location of the stored proc FLOW2XML.FEX and schema FLOWDESC.XSD is in the IBISAMP **
-* copy them to the directory for which the flow report - XML file - is created

APP COPYF IBISAMP FLOWDESC.XSD DEFAULT &APPDIR FLOWDESC.XSD DEFAULT
END

APP COPYF IBISAMP FLOW2XML.FEX DEFAULT &APPDIR FLOW2XML.FEX DEFAULT
END
-RUN

-* ******** turn off the warning messages ********
SET MSG=OFF

-SET &THISFEX='flow2xml.fex';

-SET &DATA_XFOC='fl_xfoc_' || &REQ_NAME || '.foc' ;
-SET &SYN_XFOC='fl_xfoc_' || &REQ_NAME ;
-SET &SYN_XFOC_FULL=&APPDIR || '/' || &SYN_XFOC ;

-SET &DATA_XML='fl_xml_' || &REQ_NAME ||  '.xml';
-SET &SCHEMA_FR='flowdesc.xsd';
-SET &XML_DB=&APPDIR || '/' || &DATA_XML;
-SET &SYN_XML='fl_xml_' || &REQ_NAME ;
-SET &SYN_XML_FULL=&APPDIR || '/' || &SYN_XML;

-SET &DATA_FLAT='fl_flat_' || &REQ_NAME || '.ftm' ;
-SET &SYN_FLAT='fl_flat_' || &REQ_NAME ;
-SET &SYN_FLAT_FULL=&APPDIR || '/' || &SYN_FLAT ;


SET ALL=ON
SET SHORTPATH=SQL

-* *********** create xfoc metadata for the flow ****************
TABLE FILE _EDAHOME/CATALOG/SYSFLOW 	
PRINT *
WHERE REQ_NAME EQ '&REQ_NAME.Enter Flow name:.' 	
WHERE APPDIR EQ '&APPDIR.Enter Application where flow resides:.' 	
ON TABLE SET EXTRACT ON
ON TABLE HOLD AS &SYN_XFOC_FULL FORMAT XFOCUS
END
-RUN
-IF &LINES EQ 0 GOTO :FLOW_NOT_FOUND;
-GOTO :CONT_SP;

-:FLOW_NOT_FOUND
-TYPE  Error: The specified flow is not found; an XML file will not be created.
-GOTO :ENDING;

-:CONT_SP
-* read the saved info and load into a flat file

TABLE FILE &SYN_XFOC
PRINT
APPDIR.APPDIR
REQ.REQ_NAME
REQ.AUTHOR
REQ.DESCR
REQ.MIGR_RELEASE
REQ.MIGR_GENNUM
REQ.MIGR_BLD
REQ.MODIF_DT
REQ.MODIF_TM
REQ.EMAIL_TO
REQ.JOB_TYPE
REQ.JOB_TYPN
REQ.BLK_LOAD_TYPE
REQ.BLK_LOAD_TYPN
REQ.SQL_TXT
REQ.DBMS_ENGINE
REQ.DBMS_CONN
REQ.STOP_AFT_ERR
REQ.STOP_NO_ROWS
REQ.PARSER_ERROR
REQ.NUM_ATMPT
REQ.PREF_WAK
REQ.INVALID_FLOW
REQ.INVALID_MSG
SCHED.START_DATE
SCHED.END_DATE
SCHED.EVENT_TYP
SCHED.CODE_TYP
SCHED.LAST
SCHED.DAYS_OF_WK
SCHED.START_HH
SCHED.START_MM
SCHED.FREQ
SCHED.END_HH
SCHED.END_MM
SCHED.DAYS_OF_MONTH
LOGGING.LOG_OPTION
LOGGING.LOG_APPDIR
LOGGING.LOG_SYNONYM
LOGGING.LOG_DATASET
SOURCE.SRC_NAME
SOURCE.SRC_APPDIR
SOURCE.SRC_DESCR
SOURCE.SRC_TBL
SOURCE.SRC_ENGINE
SOURCE.SRC_ADAPTER
SOURCE.SRC_CONNECT
SRCTRNSF.SRC_TRNF_NAME
SRCTRNSF.SRC_TRNF_EXP
SRCTRNSF.SRC_TRNF_USG
TARGET.TRG_NAME
TARGET.TRG_APPDIR
TARGET.TRG_IS_NEW
TARGET.TRG_STRATEGY
TARGET.TRG_ENGINE
TARGET.TRG_ADAPTER
TARGET.TRG_CONNECT
TARGET.TRG_TBL
TARGET.TRG_DATASET
TARGET.TRG_NOMATCH
TARGET.TRG_NOMATCHN
TARGET.TRG_LOADOPT
TARGET.TRG_LOADOPTN
TARGET.TRG_DROPDB
TARGET.TRG_DROPDBN
TARGET.TRG_HOLD_FMT
TARGET.TRG_COMMIT
TRGTRNSF.TRG_TRNF_NAME
TRGTRNSF.TRG_TRNF_EXP
TRGTRNSF.TRG_TRNF_USG
PROCFLOW.DEP_TYPE
PROCFLOW.DEP_TYPN
PROCDEP.DEP_RUN_TYPE
PROCDEP.DEP_RUN_TYPN
PROCDEP.DEP_ALIAS
PROCDEP.DEP_REQ
PROCDEP.DEP_REQ_APP
PROCDEP.DEP_GRP_ID
PROCWAIT.WAIT_LST
PROCWAIT.WAIT_INTERV
PROCMAIL.DEP_EMAIL_TO
PROCMAIL.DEP_EMAIL_SUBJ
PROCMAIL.DEP_EMAIL_MSG
PROCMAIL.DEP_EMAIL_ATTACH
PROCMAIL.DEP_EMAIL_FNAME
PROCMAIL.DEP_EMAIL_FTYPE
SETVAR.DEP_SET_VAR
BLKPARMS.BLK_PRPNAME
BLKPARMS.BLK_PRPVAL
ON TABLE HOLD AS &SYN_FLAT_FULL FORMAT INTERNAL
END

-* delete the xml synonym if these file (.mas, .acx) exist already ***********
DROP SYNONYM &SYN_XML_FULL
END

-* create xml synonym based on the schema ************
CREATE SYNONYM &SYN_XML_FULL
FOR &SCHEMA_FR
DATABASE  &XML_DB
DBMS XML AT &APPDIR DROP
UNIQUENAMES
END
-RUN

MODIFY FILE &SYN_XML
FIXFORM FROM &SYN_FLAT_FULL ALIAS PROPAGATE
 GOTO MATCHIT1

 CASE MATCHIT1
  COMPUTE
   APPDIR1/A64=E01;
   REQ_NAME/A64=E02;
   AUTHOR/A20=E03;
   DESCR/A78=E04;
   MIGR_RELEASE/A20=E05;
   MIGR_GENNUM/A10=E06;
   MIGR_BLD/A10=E07;
   MODIF_DT/A11=E08;
   MODIF_TM/A21=E09;
   EMAIL_TO/A50=E10;
   JOB_TYPE/A36=E11;
   JOB_TYPN/I2=E12;
   BLK_LOAD_TYPE/A8=E13;
   BLK_LOAD_TYPN/I2=E14;
   SQL_TXT/A4096=E15;
   DBMS_ENGINE/A16=E16;
   DBMS_CONN/A16=E17;
   STOP_AFT_ERR/I8=E18;
   STOP_NO_ROWS/A1=E19;
   PARSER_ERROR/A1=E20;
   NUM_ATMPT/I1=E21;
   PREF_WAK/A1=E22;
   INVALID_FLOW/A1=E23;
   INVALID_MSG/A64=E24;
   START_DATE/A8=E25;
   END_DATE/A8=E26;
   EVENT_TYP/A1=E27;
   CODE_TYP/A1=E28;
   LAST/A1=E29;
   DAYS_OF_WK/A7=E30;
   START_HH/A2=E31;
   START_MM/A2=E32;
   FREQ/A2=E33;
   END_HH/A2=E34;
   END_MM/A2=E35;
   DAYS_OF_MONTH/A31=E36;
   LOG_OPTION/A8=E37;
   LOG_APPDIR/A64=E38;
   LOG_SYNONYM/A25=E39;
   LOG_DATASET/A35=E40;
   SRC_NAME/A64=E41;
   SRC_APPDIR/A64=E42;
   SRC_DESCR/A78=E43;
   SRC_TBL/A64=E44;
   SRC_ENGINE/A8=E45;
   SRC_ADAPTER/A36=E46;
   SRC_CONNECT/A20=E47;
   SRC_TRNF_NAME/A64=E48;
   SRC_TRNF_EXP/A255=E49;
   SRC_TRNF_USG/A8=E50;
   TRG_NAME/A64=E51;
   TRG_APPDIR/A64=E52;
   TRG_IS_NEW/A1=E53;
   TRG_STRATEGY/I2=E54;
   TRG_ENGINE/A8=E55;
   TRG_ADAPTER/A36=E56;
   TRG_CONNECT/A20=E57;
   TRG_TBL/A64=E58;
   TRG_DATASET/A64=E59;
   TRG_NOMATCH/A7=E60;
   TRG_NOMATCHN/I2=E61;
   TRG_LOADOPT/A12=E62;
   TRG_LOADOPTN/I2=E63;
   TRG_DROPDB/A5=E64;
   TRG_DROPDBN/I2=E65;
   TRG_HOLD_FMT/A10=E66;
   TRG_COMMIT/I6=E67;
   TRG_TRNF_NAME/A64=E68;
   TRG_TRNF_EXP/A255=E69;
   TRG_TRNF_USG/A8=E70;
   DEP_TYPE/A9=E71;
   DEP_TYPN/I5=E72;
   DEP_RUN_TYPE/A5=E73;
   DEP_RUN_TYPN/I2=E74;
   DEP_ALIAS/A10=E75;
   DEP_REQ/A64=E76;
   DEP_REQ_APP/A64=E77;
   DEP_GRP_ID/A10=E78;
   DEP_EMAIL_TO/A50=E81;
   DEP_EMAIL_SUBJ/A50=E82;
   DEP_EMAIL_MSG/A255=E83;
   DEP_EMAIL_ATTACH/A64=E84;
   DEP_EMAIL_FNAME/A64=E85;
   DEP_EMAIL_FTYPE/A16=E86;
   DEP_SET_VAR/A255=E87;
   BLK_PRPNAME/A64=E88;
   BLK_PRPVAL/A16=E89;
  MATCH APPDIR
   ON NOMATCH INCLUDE
   ON MATCH CONTINUE
  MATCH REQ_NAME
   ON NOMATCH INCLUDE
   ON MATCH CONTINUE
  MATCH START_DATE
   ON NOMATCH INCLUDE
   ON MATCH CONTINUE
  MATCH LOG_OPTION
   ON NOMATCH INCLUDE
   ON MATCH CONTINUE
  MATCH SRC_NAME
   ON NOMATCH INCLUDE
   ON MATCH CONTINUE
  MATCH SRC_TRNF_NAME
   ON NOMATCH INCLUDE
   ON MATCH CONTINUE
  MATCH TRG_NAME
   ON NOMATCH INCLUDE
   ON MATCH CONTINUE
  MATCH TRG_TRNF_NAME
   ON NOMATCH INCLUDE
   ON MATCH CONTINUE
  MATCH DEP_TYPE
   ON NOMATCH INCLUDE
   ON MATCH CONTINUE
 MATCH WAIT_LST
   ON NOMATCH INCLUDE
   ON MATCH CONTINUE
  MATCH DEP_RUN_TYPE
   ON NOMATCH INCLUDE
   ON MATCH CONTINUE
  MATCH DEP_EMAIL_TO
   ON NOMATCH INCLUDE
   ON MATCH CONTINUE
  MATCH DEP_SET_VAR
   ON NOMATCH INCLUDE
   ON MATCH CONTINUE
  MATCH BLK_PRPNAME
   ON MATCH INCLUDE
   ON NOMATCH INCLUDE
  GOTO TOP
 ENDCASE

DATA ON &SYN_FLAT_FULL
END
-RUN

-* remove all temporary files *******************
APP DELETEFILE '&APPDIR' '&SYN_FLAT' MASTER
END
APP DELETEFILE '&APPDIR' '&DATA_FLAT' DEFAULT
END

APP DELETEFILE '&APPDIR' '&SYN_XFOC' MASTER
END
APP DELETEFILE '&APPDIR' '&DATA_XFOC' DEFAULT
END

APP DELETEFILE '&APPDIR' '&SYN_XML' MASTER
END

-* don't delete the flow2xml.fex and flowdesc.xsd from ibisamp ********
-IF &APPDIR EQ 'ibisamp' GOTO :ENDGOOD;
APP DELETEFILE '&APPDIR' '&SCHEMA_FR' DEFAULT
END

APP DELETEFILE '&APPDIR' '&THISFEX' FOCEXEC
END
-* *** finishied loading the flow info into the XML file ************
-:ENDGOOD
-TYPE The XML file fl_xml_*.xml created; located in the same directory where your flow is.

-:ENDING
