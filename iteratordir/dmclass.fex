-*-----------------------------------------------------------------------------*
-* Copyright (c) Information Builders, Inc. All rights reserved. @MFSM_NOPROLOG@
-*FEX_DESCRIPTION=DataMigrator Class Project, Create and load DMBRANCH (Branches) and DMEMP (Employee) tables
-*-----------------------------------------------------------------------------*
-DEFAULT &CMDBTYPE=SQLMSS
-DEFAULT &CONNECTION=CON01
-DEFAULT &RUNTYPE=CREATE
-IF &RUNTYPE EQ CREATE GOTO :CREATE;
-IF &RUNTYPE EQ UPDATE GOTO :UPDATE;
-IF &RUNTYPE EQ RELOAD GOTO :RELOAD;
-IF &RUNTYPE EQ DELETE GOTO :DELETE;
-TYPE RUNTYPE must be CREATE, RELOAD, UPDATE, DELETE
-GOTO :ENDERR
-*********************************************************************************
-:CREATE
-* Load DMBRANCH (Region)
EX EDADEL MASTER,dfix/iteratordir/dmbranch,IFEXIST
-RUN
EX EDADEL ACCESS,dfix/iteratordir/dmbranch,IFEXIST
-RUN
-* dmbranch.mas
EX -LINES 6 EDAPUT MASTER,dfix/iteratordir/DMBRANCH,C,FILE,
FILENAME=DMBRANCH, SUFFIX=&CMDBTYPE, $
  SEGMENT=DMBRANCH, SEGTYPE=S0, $
    FIELDNAME=BRANCH, ALIAS=branch, USAGE=A3, ACTUAL=A3, MISSING=OFF, $
    FIELDNAME=BRLOC, ALIAS=brloc, USAGE=A17, ACTUAL=A17, MISSING=ON, $
    FIELDNAME=BRSTATE, ALIAS=brstate, USAGE=A2, ACTUAL=A2, MISSING=ON, $
-RUN
-* dmbranch.acx
EX -LINES 3 EDAPUT ACCESS,dfix/iteratordir/dmbranch,C,FILE,
  SEGNAME=DMBRANCH, TABLENAME=dmbranch, CONNECTION=&CONNECTION,
  KEYS=1, WRITE=YES, $
-RUN
CREATE FILE DMBRANCH DROP
END
-RUN
-RUN
-IF (&FOCERRNUM EQ 0) GOTO :LOAD_REG;
-TYPE Failure on DMBRANCH create
-*SET &CUSTMSG = EMGPUT(18644,1,&FOCERRNUM.LENGTH,&FOCERRNUM,-0,'',0,'',80,'A80');
-GOTO :ENDERR
-:LOAD_REG
MODIFY FILE DMBRANCH
FREEFORM BRANCH BRLOC BRSTATE
MATCH BRANCH
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA
BOS,Boston,MA,$
DAL,Dallas,TX,$
LA,Cupertino,CA,$
ORL,Orlando,FL,$
STL,St. Louis,MO,$
WAS,Washington,DC,$
END
-RUN
-SET &SAMPLE_RC =
- IF (&FOCERRNUM NE 0) THEN &FOCERRNUM ELSE IF &TRANS EQ 0 THEN 18708 ELSE 0;
-IF (&SAMPLE_RC EQ 0) GOTO :EMP;
-TYPE Failure on DMEMP load. Return code = &SAMPLE_RC
-*SET &CUSTMSG = EMGPUT(18656,1,&SAMPLE_RC.LENGTH,&SAMPLE_RC, 0,'',0,'',80,'A80');
-GOTO :ENDERR
-*********************************************************************************
-:EMP
-* Load DMEMP (Employee)
EX EDADEL MASTER,dfix/iteratordir/dmemp,IFEXIST
-RUN
EX EDADEL ACCESS,dfix/iteratordir/dmemp,IFEXIST
-RUN
-* dmemp.mas
EX -LINES 12 EDAPUT MASTER,dfix/iteratordir/DMEMP,C,FILE,
FILENAME=DMEMP, SUFFIX=&CMDBTYPE, $
  SEGMENT=DMEMP, SEGTYPE=S0, $
    FIELDNAME=EMPID, ALIAS=empid, USAGE=A5, ACTUAL=A5, MISSING=OFF, $
    FIELDNAME=MGR, ALIAS=mgr, USAGE=A5, ACTUAL=A5, MISSING=ON, $
    FIELDNAME=BRANCH, ALIAS=branch, USAGE=A3, ACTUAL=A3, MISSING=ON, $
    FIELDNAME=REGION, ALIAS=region, USAGE=A5, ACTUAL=A5, MISSING=ON, $
    FIELDNAME=LAST_NAME, ALIAS=last_name, USAGE=A17, ACTUAL=A17, MISSING=ON, $
    FIELDNAME=FIRST_NAME, ALIAS=first_name, USAGE=A17, ACTUAL=A17, MISSING=ON, $
    FIELDNAME=POINTS, ALIAS=points, USAGE=I5, ACTUAL=I2, $
    FIELDNAME=HIRE_DATE, ALIAS=hire_date, USAGE=YYMD, ACTUAL=DATE, $
    FIELDNAME=TERM_DATE, ALIAS=term_date, USAGE=YYMD, ACTUAL=DATE, MISSING=ON, $
-RUN
-* dmemp.acx
EX -LINES 3 EDAPUT ACCESS,dfix/iteratordir/dmemp,C,FILE,
  SEGNAME=DMEMP, TABLENAME=dmemp, CONNECTION=&CONNECTION,
  KEYS=1, WRITE=YES, $
-RUN
CREATE FILE DMEMP DROP
END
-RUN
-RUN
-IF (&FOCERRNUM EQ 0) GOTO :LOAD;
-*TYPE Failure on DMEMP create
-SET &CUSTMSG = EMGPUT(18644,1,&FOCERRNUM.LENGTH,&FOCERRNUM,
-0,'',0,'',80,'A80');
-GOTO :ENDERR
-:RELOAD
-* Restore to Day One w/o dropping and recreating table, just remove rows.
SQL
  TRUNCATE TABLE DMEMP
END
-*********************************************************************************
-:LOAD
-* Load Day One Data into DMEMP
MODIFY FILE DMEMP
FREEFORM  EMPID MGR BRANCH REGION LAST_NAME FIRST_NAME POINTS HIRE_DATE TERM_DATE
COMPUTE TERM_DATE = IF TERM_DATE EQ '' THEN MISSING ELSE TERM_DATE;
COMPUTE MGR = IF MGR EQ '' THEN MISSING ELSE MGR;
MATCH EMPID
ON MATCH REJECT
ON NOMATCH INCLUDE
DATA
DD001,     ,HQ,Head,Dyer,Dan,15,20010201,,$
MG012,DD001,LA,West,Guille,Marc,10,20020228,,$
JO022,DD001,BOS,East,Osborn,John,8,20030319,,$
MR033,MG012,DAL,West,Ragel,Mark,3,20020220,,$
CE045,MG012,DAL,West,Ebner,Christian,5,20030430,,$
BF047,JO022,STL,East,Flynn,Bill,6,20020212,,$
VL052,MG012,DAL,West,Larens,Valerie,7,20030315,,$
JG088,JO022,BOS,East,Gamola,Jan,5,20020615,,$
RI103,JO022,STL,East,Iddles,Roger,8,20010105,,$
RM108,RI103,STL,East,Mendez,Roberto,1,20020420,,$
WL115,MG012,DAL,West,Laimer,Werne,2,20030320,,$
PP012,JO022,STL,East,Persson,Per,4,20030110,,$
HP431,JO022,ORL,East,Padilla,Hector,1,20030110,,$
END
-RUN
-SET &SAMPLE_RC =
- IF (&FOCERRNUM NE 0) THEN &FOCERRNUM ELSE IF &TRANS EQ 0 THEN 18708 ELSE 0;
-IF (&SAMPLE_RC EQ 0) GOTO :ENDJOB;
-TYPE Failure on DMEMP load. Return code = &SAMPLE_RC
-*SET &CUSTMSG = EMGPUT(18656,1,&SAMPLE_RC.LENGTH,&SAMPLE_RC,0,'',0,'',80,'A80');
-GOTO :ENDERR
-* Load Day Two Data
-:UPDATE
-*********************************************************************************
-* Load day TWO data into DMEMP
MODIFY FILE DMEMP
FREEFORM  EMPID MGR BRANCH REGION LAST_NAME FIRST_NAME POINTS HIRE_DATE TERM_DATE
COMPUTE TERM_DATE = IF TERM_DATE EQ '' THEN MISSING ELSE TERM_DATE;
MATCH EMPID
ON MATCH UPDATE *
ON NOMATCH INCLUDE
DATA
BF047,JO022,LA,West,Flynn,Bill,8,20020212,,$
VL052,JO022,LA,West,Iddles,Valerie,9,20030315,,$
RI103,JO022,LA,West,Iddles,Roger,10,20010105,,$
JG088,JO022,BOS,East,Newcomb,Jan,5,20020615,,$
RM108,RI103,STL,East,Mendez,Roberto,1,20020420,20030403,$
AM120,JO022,STL,East,McKay,Alex,1,20030501,,$
DH150,JO022,STL,East,Hagman,Daniel,1,20030501,,$
PP012,JO022,STL,East,Persson,Peter,6,20030110,,$
HP431,JO022,ORL,South,Padilla,Hector,2,20030110,,$
END
-RUN
-SET &SAMPLE_RC =
- IF (&FOCERRNUM NE 0) THEN &FOCERRNUM ELSE IF &TRANS EQ 0 THEN 18708 ELSE 0;
-IF (&SAMPLE_RC EQ 0) GOTO :ENDJOB;
-TYPE Failure on DMEMP load. Return code = &SAMPLE_RC
-* SET &CUSTMSG = EMGPUT(18656,1,&SAMPLE_RC.LENGTH,&SAMPLE_RC,0,'',0,'',80,'A80');
-GOTO :ENDERR
-:DELETE
-* Delete 2 rows from emp table
MODIFY FILE DMEMP
FREEFORM  EMPID
MATCH EMPID
ON MATCH DELETE
DATA
RM108,$
HP431,$
END
-RUN
-SET &SAMPLE_RC =
-IF (&FOCERRNUM NE 0) THEN &FOCERRNUM ELSE IF &TRANS EQ 0 THEN 18708 ELSE 0;
-IF (&SAMPLE_RC EQ 0) GOTO :ENDJOB;
-TYPE Failure on DMEMP delete rows. Return code = &SAMPLE_RC
-* SET &CUSTMSG = EMGPUT(18656,1,&SAMPLE_RC.LENGTH,&SAMPLE_RC,0,'',0,'',80,'A80');
-GOTO :ENDERR
-:ENDJOB
-TYPE &RUNTYPE for DMCLASS tables complete.
-:ENDERR
-IF (&CMDBTYPE EQ 'FOC') OR (&CMDBTYPE EQ 'FIX')  GOTO :EXIT;
SQL &CMDBTYPE
COMMIT;
END
-RUN
-:EXIT
